apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
//apply plugin: 'com.google.firebase.firebase-perf'

def versionMajor = 0
def versionMinor = 6
def versionPatch = 1

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "com.rosterloh.andriot"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "com.rosterloh.andriot.ThingsTestRunner"
        buildConfigField "String", "NEARBY_SERVICE_ID", "\"${deps.nearby_service_id}\""
    }
    signingConfigs {
        debug {
            keyAlias 'thingsdebugkey'
            keyPassword 'android'
            storeFile file('../debug.keystore')
            storePassword 'android'
        }
    }
    buildTypes {
        debug {
            versionNameSuffix = '.d'
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            crunchPngs true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    dataBinding {
        enabled = true
    }
}

repositories {
    maven { url "https://repo.eclipse.org/content/repositories/paho-snapshots/" }
}

dependencies {
    implementation project(path: ':common')
    implementation deps.androidthings
    implementation 'com.github.knobtviker:bme680:1.0.6'
    // Support
    implementation deps.support.app_compat
    implementation deps.support.cardview
    implementation deps.support.design
    implementation deps.constraint_layout
    // Play Services
    implementation(deps.play_services.crashlytics) { transitive = true }
    implementation deps.play_services.nearby
    implementation deps.play_services.firebase
    // Architecture components
    implementation deps.room.runtime
    annotationProcessor deps.room.compiler
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    implementation deps.lifecycle.java8
    annotationProcessor deps.lifecycle.compiler
    // Dagger
    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    annotationProcessor deps.dagger.android_support_compiler
    annotationProcessor deps.dagger.compiler

    implementation deps.retrofit.runtime
    implementation deps.retrofit.gson
    implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.1-LOCAL"
    implementation "org.eclipse.paho:org.eclipse.paho.android.service:1.1.2-SNAPSHOT"
    implementation deps.timber
    implementation 'io.jsonwebtoken:jjwt:0.9.0'
    implementation "com.github.PhilJay:MPAndroidChart:v3.0.3"
    // Testing
    testImplementation deps.junit
    testImplementation deps.hamcrest.library
    androidTestImplementation deps.support.app_compat
    androidTestImplementation(deps.espresso.core, {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestImplementation deps.arch_core.testing
    androidTestImplementation deps.mockito.android
}

apply plugin: 'com.google.gms.google-services'