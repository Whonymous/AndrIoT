apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 2

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "com.rosterloh.thingsclient"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "com.rosterloh.thingsclient.ClientTestRunner"
        resValue "string", "maps_api_key", "$System.env.MAPS_API_KEY"
        buildConfigField "String", "NEARBY_SERVICE_ID", "\"${deps.nearby_service_id}\""
    }
    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('../debug.keystore')
            storePassword 'android'
        }
    }
    buildTypes {
        debug {
            versionNameSuffix = '.d'
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            crunchPngs true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation project(path: ':common')
    // Support
    implementation deps.support.app_compat
    implementation deps.support.cardview
    implementation deps.support.design
    implementation deps.support.recyclerview
    implementation deps.constraint_layout
    // Play Services
    implementation(deps.play_services.crashlytics) { transitive = true }
    implementation deps.play_services.location
    implementation deps.play_services.maps
    implementation deps.play_services.nearby
    implementation deps.play_services.firebase
    // Architecture components
    implementation deps.room.runtime
    annotationProcessor deps.room.compiler
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    implementation deps.lifecycle.java8
    annotationProcessor deps.lifecycle.compiler
    // Dagger
    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    annotationProcessor deps.dagger.android_support_compiler
    annotationProcessor deps.dagger.compiler

    implementation deps.timber
    // Testing
    testImplementation deps.junit
    testImplementation (deps.arch_core.testing, {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })

    androidTestImplementation deps.support.app_compat
    androidTestImplementation deps.support.recyclerview
    androidTestImplementation deps.support.cardview
    androidTestImplementation deps.support.design
    androidTestImplementation(deps.espresso.core, {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestImplementation(deps.espresso.contrib, {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestImplementation(deps.arch_core.testing, {
    })
    androidTestImplementation deps.hamcrest.library
    androidTestImplementation deps.mockito.core, { exclude group: 'net.bytebuddy' }
}
